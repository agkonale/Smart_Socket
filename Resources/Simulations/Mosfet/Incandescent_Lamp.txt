*****************************************************************
** INCANDESCENT LAMP SUBCIRCUIT
*VO=NOMINAL OPERATING VOLTAGE
*IO=NOMINAL STEADY STATE OPERATING CURRENT
*RCOLD=FILAMEMT RESISTANCE MEASURED AT ROOM TEMP (300K)
*TAU=CURRENT TIME CONSTANT AFTER A 0 (zero) TO VO STEP IS APPLIED
.SUBCKT LAMP 1 2 PARAMS: VO=28 IO=25m RCOLD=112 TAU=22m TAMB=300
H1 6 0 VML 1
RH1 6 0 1
GP 0 4 VALUE={V(6)*V(1,2)}
*V(4,0) = FILAMENT TEMPERATURE IN KELVINS
RT 4 5 {300*(VO-IO*RCOLD)/(IO*IO*VO*RCOLD)}
CT 4 5 {TAU*IO*IO*VO*RCOLD/(300*(VO-IO*RCOLD))}
VAMB 5 0 {TAMB}
El 7 0 1 2 300
R1 7 0 1
E2 8 0 VALUE={V(4)*RCOLD}
R2 8 0 1
E3 10 0 7 9 10MEG
R3 10 0 1
E4 9 0 VALUE={V(8)*V(10)}
R4 9 0 1
GR 1 3 10 0 1
VML 3 2 0
.ENDS LAMP
*****************************************************************
** REPOSTING OF THIS FILE MUST INCLUDE THE FOLLOWING ACKNOWLEDGEMENT:
** COURTESY OF JIM THOMPSON, <http://www.analog-innovations.com/>
******************************************************************
